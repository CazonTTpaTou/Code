/*
-- remodéliser une table produit pour gérer les versions de données

-- créer 4 tables comme suit :
T_PRODUIT_PRD --> PRD_ID auto incrémenté
T_PRODUIT_DESIGNATION --> PRD_ID, DESIGNATION, DATE_APPLICATION
T_PRODUIT_PRIX --> PRD_ID, PRIX_HT, DATE_APPLICATION
T_PRODUIT_TVA --> PRD_ID, TAUX_TVA, DATE_APPLICATION
*/

CREATE DATABASE DB_TEST_DEV
GO

USE DB_TEST_DEV
GO

CREATE FUNCTION F_OVERLAPS (@DD1 DATETIME2, @DF1 DATETIME2, @DD2 DATETIME2, @DF2 DATETIME2)
RETURNS BIT
AS
BEGIN
    IF @DD2 >= @DF1 RETURN 0;
    IF @DF2 <= @DD1 RETURN 0;
    RETURN 1;
END;
GO

CREATE TABLE T_PRODUIT_PRD
(PRD_ID           INT IDENTITY PRIMARY KEY);
GO

CREATE VIEW V_PRD
AS
SELECT PRD_ID
FROM   T_PRODUIT_PRD

CREATE TABLE T_PRODUIT_DESIGNATION
(PRD_ID           INT REFERENCES T_PRODUIT_PRD (PRD_ID) ON DELETE CASCADE,
 PRD_DESIGNATION  VARCHAR(250) NOT NULL,
 PRD_D_DES        DATE NOT NULL DEFAULT GETDATE(),
 PRIMARY KEY (PRD_ID, PRD_D_DES));
GO

CREATE TABLE T_PRODUIT_PRIX
(PRD_ID           INT REFERENCES T_PRODUIT_PRD (PRD_ID) ON DELETE CASCADE,
 PRD_PRIX         DECIMAL(16,2) NOT NULL,
 PRD_D_PRX        DATE NOT NULL DEFAULT GETDATE(),
 PRIMARY KEY (PRD_ID, PRD_D_PRX));
GO

CREATE TABLE T_PRODUIT_TVA
(PRD_ID           INT REFERENCES T_PRODUIT_PRD (PRD_ID) ON DELETE CASCADE,
 PRD_TVA          FLOAT NOT NULL,
 PRD_D_TVA        DATE NOT NULL DEFAULT GETDATE(),
 PRIMARY KEY (PRD_ID, PRD_D_TVA));
GO

CREATE VIEW V_PRODUIT_DESIGNATION
AS
SELECT PRD_ID, PRD_DESIGNATION, PRD_D_DES AS PRD_DES_DEBUT,
       COALESCE((SELECT MIN(PRD_D_DES)
                 FROM   T_PRODUIT_DESIGNATION AS Tin
                 WHERE  Tin.PRD_ID = Tout.PRD_ID
                   AND  Tin.PRD_D_DES > Tout.PRD_D_DES), '9999-12-31') AS PRD_DES_FIN
FROM   T_PRODUIT_DESIGNATION AS Tout
GO

CREATE VIEW V_PRODUIT_PRIX
AS
SELECT PRD_ID, PRD_PRIX, PRD_D_PRX AS PRD_PRX_DEBUT,
       COALESCE((SELECT MIN(PRD_D_PRX)
                 FROM   T_PRODUIT_PRIX AS Tin
                 WHERE  Tin.PRD_ID = Tout.PRD_ID
                   AND  Tin.PRD_D_PRX > Tout.PRD_D_PRX), '9999-12-31') AS PRD_PRX_FIN
FROM   T_PRODUIT_PRIX AS Tout
GO

CREATE VIEW V_PRODUIT_TVA
AS
SELECT PRD_ID, PRD_TVA, PRD_D_TVA AS PRD_TVA_DEBUT,
       COALESCE((SELECT MIN(PRD_D_TVA)
                 FROM   T_PRODUIT_TVA AS Tin
                 WHERE  Tin.PRD_ID = Tout.PRD_ID
                   AND  Tin.PRD_D_TVA > Tout.PRD_D_TVA), '9999-12-31') AS PRD_TVA_FIN
FROM   T_PRODUIT_TVA AS Tout
GO
/*
DECLARE @NEWID INT;
INSERT INTO [dbo].[T_PRODUIT_PRD] DEFAULT VALUES;
SET @NEWID = SCOPE_IDENTITY();
INSERT INTO T_PRODUIT_PRIX VALUES (@NEWID, 123.45, '2012-01-01');
INSERT INTO T_PRODUIT_DESIGNATION VALUES (@NEWID, 'Pelle à gâteaux', '2012-01-06');
INSERT INTO T_PRODUIT_TVA VALUES (@NEWID, 17.6, '2012-01-01');
INSERT INTO T_PRODUIT_PRIX VALUES (@NEWID, 125.00, '2014-07-01');
INSERT INTO T_PRODUIT_PRIX VALUES (@NEWID, 130.00, '2015-09-01');
INSERT INTO T_PRODUIT_TVA VALUES (@NEWID, 19.6, '2013-06-30');
*/

CREATE VIEW V_PRODUIT_HISTORIQUE
AS
WITH T AS
(
SELECT PRD_ID, PRD_DES_DEBUT AS D
FROM   V_PRODUIT_DESIGNATION
UNION ALL
SELECT PRD_ID, PRD_DES_FIN AS D
FROM   V_PRODUIT_DESIGNATION
UNION ALL
SELECT PRD_ID, PRD_PRX_DEBUT AS D
FROM   V_PRODUIT_PRIX
UNION ALL 
SELECT PRD_ID, PRD_PRX_FIN AS D
FROM   V_PRODUIT_PRIX
UNION ALL 
SELECT PRD_ID, PRD_TVA_DEBUT AS D
FROM   V_PRODUIT_TVA
UNION ALL
SELECT PRD_ID, PRD_TVA_FIN AS D
FROM   V_PRODUIT_TVA
),
T_TRANCHES AS
(
SELECT DISTINCT PRD_ID, T1.D AS DD, 
       (SELECT  MIN(T2.D)
        FROM    T AS T2
        WHERE   T1.D < T2.D) AS DF
FROM   T AS T1
WHERE  T1.D < '9999-12-31'
)
SELECT P.PRD_ID, DD AS DATE_DEBUT, DF AS DATE_FIN, 
       PRD_DESIGNATION, PRD_PRIX, PRD_TVA
FROM T_TRANCHES AS P
     LEFT OUTER JOIN V_PRODUIT_DESIGNATION AS PD
           ON P.PRD_ID = PD.PRD_ID
           AND dbo.F_OVERLAPS(DD, DF, PD.PRD_DES_DEBUT, PD.PRD_DES_FIN) = 1
     LEFT OUTER JOIN V_PRODUIT_PRIX AS PP 
           ON P.PRD_ID = PP.PRD_ID
           AND dbo.F_OVERLAPS(DD, DF, PP.PRD_PRX_DEBUT, PP.PRD_PRX_FIN) = 1
     LEFT OUTER JOIN V_PRODUIT_TVA AS PT
           ON P.PRD_ID = PT.PRD_ID
           AND dbo.F_OVERLAPS(DD, DF, PT.PRD_TVA_DEBUT, PT.PRD_TVA_FIN) = 1;
GO


CREATE VIEW V_PRODUIT
AS
SELECT *
FROM   V_PRODUIT_HISTORIQUE
WHERE  GETDATE() BETWEEN DATE_DEBUT AND DATE_FIN
GO

SELECT * FROM V_PRODUIT

/* 
SELECT * FROM V_PRODUIT_TVA
SELECT * FROM V_PRODUIT_PRIX
SELECT * FROM V_PRODUIT_DESIGNATION
SELECT * FROM V_PRODUIT_HISTORIQUE
*/